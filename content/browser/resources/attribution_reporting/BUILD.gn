# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/browser/resources/tools/build_webui.gni")

build_webui("build") {
  grd_prefix = "attribution_internals"

  static_files = [
    "attribution_internals.html",
    "attribution_internals.css",
  ]

  non_web_component_files = [
    "attribution_internals.ts",
    "table_model.ts",
  ]

  web_component_files = [ "attribution_internals_table.ts" ]

  ts_deps = [
    "//ui/webui/resources:library",
    "//ui/webui/resources/mojo:library",
  ]

  mojom_folder = "$root_gen_dir/mojom-webui"
  attribution_reporting_component_folder = "components/attribution_reporting"
  attribution_reporting_content_folder = "content/browser/attribution_reporting"
  network_folder = "services/network/public/mojom"
  aggregation_service_folder = "components/aggregation_service"

  mojo_files = [
    "$mojom_folder/$aggregation_service_folder/aggregation_service.mojom-webui.js",
    "$mojom_folder/$attribution_reporting_component_folder/registration.mojom-webui.js",
    "$mojom_folder/$attribution_reporting_component_folder/source_registration_error.mojom-webui.js",
    "$mojom_folder/$attribution_reporting_content_folder/attribution_internals.mojom-webui.js",
    "$mojom_folder/$attribution_reporting_content_folder/attribution_reporting.mojom-webui.js",
    "$mojom_folder/$network_folder/schemeful_site.mojom-webui.js",
  ]

  mojo_files_deps = [
    "//$aggregation_service_folder:mojom_js__generator",
    "//$attribution_reporting_component_folder:mojom_js__generator",
    "//$attribution_reporting_component_folder:registration_mojom_js__generator",
    "//$attribution_reporting_content_folder:internals_mojo_bindings_js__generator",
    "//$attribution_reporting_content_folder:mojo_bindings_js__generator",
    "//$network_folder:mojom_schemeful_site_js__generator",
  ]

  grit_output_dir = "$root_gen_dir/content"
  html_to_wrapper_template = "native"
}
