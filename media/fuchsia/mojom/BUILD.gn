# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

mojom("fuchsia_media") {
  sources = [ "fuchsia_media.mojom" ]
  deps = [ "//media/mojo/mojom" ]

  export_class_attribute_blink = "BLINK_PLATFORM_EXPORT"
  export_define_blink = "BLINK_PLATFORM_IMPLEMENTATION=1"
  export_header_blink = "third_party/blink/public/platform/web_common.h"

  export_class_attribute = "MEDIA_EXPORT"
  export_define = "IS_MEDIA_IMPL=1"
  export_header = "media/base/media_export.h"

  shared_cpp_typemaps = {
    types = [
      {
        mojom = "media.mojom.CdmRequest"
        cpp = "::fidl::InterfaceRequest<::fuchsia::media::drm::ContentDecryptionModule>"
        move_only = true
      },
      {
        mojom = "media.mojom.StreamProcessorRequest"
        cpp = "::fidl::InterfaceRequest<::fuchsia::media::StreamProcessor>"
        move_only = true
      },
    ]
    traits_headers = [ "fuchsia_media_mojom_traits.h" ]
    traits_public_deps = [
      "//mojo/public/cpp/base/fuchsia:traits",
      "//third_party/fuchsia-sdk/sdk/fidl/fuchsia.media",
      "//third_party/fuchsia-sdk/sdk/fidl/fuchsia.media.drm",
    ]
  }

  cpp_typemaps = [ shared_cpp_typemaps ]
  blink_cpp_typemaps = [ shared_cpp_typemaps ]
}
