# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//crypto/features.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/include_polymer.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

generate_grd("build_grd") {
  grd_prefix = "webui"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"

  input_files = [ "test_loader.html" ]
  input_files_base_dir = rebase_path(".", "//")

  if (!is_chromeos_ash && !is_android) {
    # Roboto Font. Roboto-Regular and Roboto-Light is already available on
    # Android, and Roboto-Medium is not used on Android. All 6 weights of
    # Roboto are available on Chrome OS.
    input_files += [
      "roboto/roboto-bold.woff2",
      "roboto/roboto-medium.woff2",
      "roboto/roboto-regular.woff2",
    ]
  }

  public_deps = [
    "cr_elements:build_grdp",
    "css:build_grdp",
    "images:build_grdp",
    "js:build_grdp",
    "js/metrics_reporter:build_grdp",
    "//third_party/jstemplate:build_grdp",
  ]
  grdp_files = [
    "$root_gen_dir/third_party/jstemplate/resources.grdp",
    "$target_gen_dir/cr_elements/resources.grdp",
    "$target_gen_dir/css/resources.grdp",
    "$target_gen_dir/images/resources.grdp",
    "$target_gen_dir/js/metrics_reporter/resources.grdp",
    "$target_gen_dir/js/resources.grdp",
  ]

  if (include_polymer) {
    public_deps += [
      "cr_components/app_management:build_grdp",
      "cr_components/customize_themes:build_grdp",
      "cr_components/help_bubble:build_grdp",
      "cr_components/localized_link:build_grdp",
      "cr_components/managed_dialog:build_grdp",
      "cr_components/managed_footnote:build_grdp",
      "cr_components/most_visited:build_grdp",
      "js/browser_command:build_grdp",
      "//third_party/polymer/v3_0:build_grdp",
    ]
    grdp_files += [
      "$root_gen_dir/third_party/polymer/v3_0/polymer_3_0_resources.grdp",
      "$target_gen_dir/cr_components/app_management/resources.grdp",
      "$target_gen_dir/cr_components/customize_themes/resources.grdp",
      "$target_gen_dir/cr_components/help_bubble/resources.grdp",
      "$target_gen_dir/cr_components/localized_link/resources.grdp",
      "$target_gen_dir/cr_components/managed_dialog/resources.grdp",
      "$target_gen_dir/cr_components/managed_footnote/resources.grdp",
      "$target_gen_dir/cr_components/most_visited/resources.grdp",
      "$target_gen_dir/js/browser_command/resources.grdp",
    ]

    if (use_nss_certs) {
      public_deps += [ "cr_components/certificate_manager:build_grdp" ]
      grdp_files +=
          [ "$target_gen_dir/cr_components/certificate_manager/resources.grdp" ]
    }

    if (!is_android) {
      public_deps += [
        "cr_components/history_clusters:build_grdp",
        "cr_components/omnibox:build_grdp",
        "cr_components/omnibox/icons:build_grdp",
      ]
      grdp_files += [
        "$target_gen_dir/cr_components/history_clusters/resources.grdp",
        "$target_gen_dir/cr_components/omnibox/resources.grdp",
        "$target_gen_dir/cr_components/omnibox/icons/resources.grdp",
      ]

      if (is_chrome_branded) {
        public_deps +=
            [ "cr_components/omnibox/icons:chrome_branded_build_grdp" ]
        grdp_files += [ "$target_gen_dir/cr_components/omnibox/icons/chrome_branded_resources.grdp" ]
      }
    }
  }

  if (is_chromeos_ash) {
    public_deps += [
      ":build_cros_styles_grdp",
      "//ash/webui/common/resources:build_grdp",
      "//ash/webui/common/resources:build_html_css_wrapper_files_grdp",
      "//third_party/material_web_components:build_grdp",
      "//third_party/web-animations-js:build_grdp",
    ]
    grdp_files += [
      "$root_gen_dir/ash/webui/common/resources/ash_common_resources.grdp",
      "$root_gen_dir/ash/webui/common/resources/html_css_wrapper_files_resources.grdp",
      "$root_gen_dir/third_party/material_web_components/material_web_components_resources.grdp",
      "$root_gen_dir/third_party/web-animations-js/web_animations_resources.grdp",
      "$target_gen_dir/cros_styles_resources.grdp",
    ]
  }

  if (!is_android && !is_ios) {
    public_deps += [
      "cr_components/color_change_listener:build_grdp",
      "//third_party/lottie:build_grdp",
    ]
    grdp_files += [
      "$root_gen_dir/third_party/lottie/resources.grdp",
      "$root_gen_dir/ui/webui/resources/cr_components/color_change_listener/resources.grdp",
    ]
  }
}

if (is_chromeos_ash) {
  generate_grd("build_cros_styles_grdp") {
    grd_prefix = "webui"
    out_grd = "$target_gen_dir/cros_styles_resources.grdp"
    public_deps = [ "//ui/chromeos/styles:cros_styles_css" ]
    input_files_base_dir = rebase_path("$root_gen_dir/", root_build_dir)
    input_files = [ "ui/chromeos/styles/cros_styles.css" ]
    resource_path_rewrites =
        [ "ui/chromeos/styles/cros_styles.css|chromeos/colors/cros_styles.css" ]
  }
}

# TypeScript targets

# TODO(crbug/1402829): This target has no input files and just exists to allow
# incrementally rolling out the breaking up of this target to separate targets.
# Remove when all references have been updated.
ignored_folder = "$target_gen_dir/ignored"
ts_library("library") {
  root_dir = ignored_folder
  out_dir = ignored_folder
  composite = true
  tsconfig_base = "tsconfig_base.json"

  # Intentionally empty. Don't add any files here.
  in_files = []

  deps = [
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
    "//ui/webui/resources/mojo:library",
  ]
}
