# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")
import("//ui/webui/webui_features.gni")
import("../../tools/build_cr_component.gni")

assert(!is_android)

mojom("mojo_bindings") {
  sources = [ "history_clusters.mojom" ]
  public_deps = [
    "//mojo/public/mojom/base",
    "//ui/base/mojom",
    "//ui/gfx/geometry/mojom",
    "//url/mojom:url_mojom_gurl",
  ]
  webui_module_path = "chrome://resources/cr_components/history_clusters/"
  use_typescript_sources = true
}

build_cr_component("build") {
  grd_prefix = "cr_components_history_clusters"

  web_component_files = [
    "cluster.ts",
    "clusters.ts",
    "cluster_menu.ts",
    "page_favicon.ts",
    "search_query.ts",
    "url_visit.ts",
  ]

  non_web_component_files = [
    "browser_proxy.ts",
    "metrics_proxy.ts",
    "utils.ts",
  ]

  css_files = [
    "history_clusters_shared_style.css",
    "shared_vars.css",
  ]

  static_files = [ "thumb_down_filled_gm_grey.svg" ]

  mojo_files_deps = [ ":mojo_bindings_ts__generator" ]
  mojo_files = [ "$target_gen_dir/history_clusters.mojom-webui.ts" ]

  tsc_dir =
      "$root_gen_dir/ui/webui/resources/tsc/cr_components/history_clusters"
  ts_definitions = [ "//tools/typescript/definitions/metrics_private.d.ts" ]
  ts_deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources:library",
    "//ui/webui/resources/mojo:library",
  ]

  optimize = optimize_webui
}
