# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//third_party/protobuf/proto_library.gni")

proto_library("translate_kit_api_proto") {
  sources = [ "proto/translate_kit_api.proto" ]
}

source_set("on_device_translation_service") {
  sources = [
    "on_device_translation_service.cc",
    "on_device_translation_service.h",
    "translate_kit_client.cc",
    "translate_kit_client.h",
    "translate_kit_structs.h",
  ]
  deps = [ ":translate_kit_api_proto" ]
  public_deps = [
    "//base",
    "//components/services/on_device_translation/public/cpp",
    "//components/services/on_device_translation/public/mojom",
    "//mojo/public/cpp/bindings",
  ]

  if (is_linux) {
    sources += [
      "sandbox_hook.cc",
      "sandbox_hook.h",
    ]
    deps += [ "//sandbox/linux:sandbox_services" ]
    public_deps += [ "//sandbox/policy" ]
  }
}

source_set("unit_tests") {
  testonly = true

  sources = [ "on_device_translation_service_unittest.cc" ]
  deps = [
    "//base/test:test_support",
    "//components/services/on_device_translation:on_device_translation_service",
    "//components/services/on_device_translation/public/cpp",
    "//components/services/on_device_translation/public/mojom",
    "//testing/gmock",
    "//testing/gtest",
  ]
  data_deps = [ ":mock_translate_kit_lib" ]
}

shared_library("mock_translate_kit_lib") {
  testonly = true
  sources = [
    "mock_translate_kit_lib.cc",
    "translate_kit_structs.h",
  ]
  public_deps = [ "//base" ]
}
