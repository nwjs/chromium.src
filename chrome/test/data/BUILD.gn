# Copyright 2015 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//tools/grit/grit_rule.gni")

grit("chrome_test_resources") {
  source = "chrome_test_resources.grd"
  defines = chrome_grit_defines
  outputs = [
    "test/data/grit/chrome_test_resources.h",
    "chrome_test_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

grit("webui_test_resources") {
  source = "webui_test_resources.grd"
  defines = chrome_grit_defines
  outputs = [
    "test/data/grit/webui_test_resources.h",
    "webui_test_resources.pak",
  ]

  deps = [
    ":web_ui_test_bindings_js__generator",
    ":web_ui_test_mojom_js_module",
  ]

  output_dir = "$root_gen_dir/chrome"
}

mojom("web_ui_test_bindings") {
  sources = [
    "webui/mojo/foobar.mojom",
    "webui/mojo/mojo_file_system_access_test.mojom",
    "webui/web_ui_test.mojom",
  ]
  webui_module_path = "/"
}

copy("web_ui_test_mojom_js_module") {
  sources = [
    "$root_gen_dir/mojom-webui/chrome/test/data/webui/mojo/foobar.mojom-webui.js",
    "$root_gen_dir/mojom-webui/chrome/test/data/webui/mojo/mojo_file_system_access_test.mojom-webui.js",
    "$root_gen_dir/mojom-webui/chrome/test/data/webui/web_ui_test.mojom-webui.js",
  ]
  outputs = [ "$target_gen_dir/webui/{{source_file_part}}" ]
  deps = [ ":web_ui_test_bindings_js__generator" ]
}
