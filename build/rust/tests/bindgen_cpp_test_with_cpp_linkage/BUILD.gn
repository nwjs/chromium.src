# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rust_bindgen.gni")
import("//build/rust/rust_static_library.gni")

source_set("cpp_lib") {
  sources = [
    "cpp.cc",
    "cpp.h",
  ]
}

rust_bindgen("cpp_lib_bindgen") {
  deps = [ ":cpp_lib" ]
  header = "cpp.h"
  cpp = true
}

rust_static_library("rust_lib") {
  deps = [
    ":cpp_lib",
    ":cpp_lib_bindgen",
  ]
  sources = [ "lib.rs" ]
  crate_root = "lib.rs"

  allow_unsafe = true  # Needed for FFI.

  bindgen_output = get_target_outputs(":cpp_lib_bindgen")
  inputs = bindgen_output
  rustenv = [ "BINDGEN_RS_FILE=" +
              rebase_path(bindgen_output[0], get_path_info(crate_root, "dir")) ]
}

executable("bindgen_cpp_test_with_cpp_linkage") {
  sources = [ "main.cc" ]
  deps = [ ":rust_lib" ]
}
